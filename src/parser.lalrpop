use ast::*;
use std::str::FromStr;

grammar;

//Macros
List<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};


//Terminals
Num: i32 = <s:r"-?[0-9]+"> => i32::from_str(s).unwrap();
Ident: String = <r"[a-zA-Z][a-zA-Z0-9_]*"> => <>.to_string();

pub Module: Module = {
    <Decl+> => Module::new(<>)
};

pub Decl: Decl = {
    "extern" <name:Ident> "(" <params:List<Param>> ")" "->" <ty:Type> ";" =>
        Decl::external(name, params, ty),
    "extern" <name:Ident> "()" "->" <ty:Type> ";" =>
        Decl::external(name, vec![], ty),
    Func => { let (name, lam) = <>; Decl::Func(name, lam) },
};

pub LamTyVars: Vec<String> = {
    "<" <tyvars: List<Ident>> ">" => tyvars,
                                  => vec![]
};

pub Func: (String,Lam) = {
    "fn" <name:Ident> "(" <p:List<Ident>> ")" "{" <body: Expr> "}" => {
        (name, Lam::new(p, body))
    },
    "fn" <name:Ident> "()" "{" <body: Expr> "}" => {
        (name, Lam::new(vec![], body))
    }
};

pub Param: (String,Type) = {
    <id:Ident> ":" <ty:Type> => (id,ty)
};

pub Var: Expr = {
    Ident   => Expr::Var(<>),
    //<Ident> "<" <List<Type>> ">" => Expr::Var(<>),
};

pub Callee: Expr = {
    Var   => <>
};

pub Expr: Expr = {
    Num     => Expr::I32Lit(<>),
    "true"  => Expr::BoolLit(true),
    "false" => Expr::BoolLit(false),
    "()"    => Expr::UnitLit,
    //Ident   => Expr::Var(<>),
    Var     => <>,
    "(" <t:Expr> ")" => t,
    "if" <cond:Expr> "{" <true_expr:Expr> "}" "else" "{" <false_expr:Expr> "}"
        => Expr::If(Box::new(If::new(cond, true_expr, false_expr))),
    <callee:Callee> "(" <args:List<Expr>> ")" 
        => mk_app(callee, args),
    "let" <id:Ident> "=" <e1:Expr> ";" <e2:Expr>
        => Expr::Let(id, Box::new(e1), Box::new(e2)),
};

BaseType: Type = {
    "()"   => Type::TyCon("unit".to_string()),
    Ident  => Type::TyCon(<>)
};

Type: Type = {
    BaseType  => <>,
    ArrowType => <>
};

ArrowType: Type = {
    <p:Type> "->" <r:BaseType>               => make_func(vec![p], r),
    "(" <p:List<Type>> ")" "->" <r:BaseType> => make_func(p, r),
};

