#![cfg_attr(feature = "cargo-clippy", allow(clippy))]
use ast::*;
use std::str::FromStr;
use ::typing::Kind;

grammar;

//Macros
List<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};


//Terminals
Num: i32 = <s:r"-?[0-9]+"> => i32::from_str(s).unwrap();
Ident: String = <r"[a-zA-Z][a-zA-Z0-9_]*'*"> => <>.to_string();

pub Module: Module = {
    <Decl+> => Module::new(<>)
};

pub Decl: Decl = {
    "extern" <name:Ident> "(" <params:List<Param>> ")" "->" <ty:Type> ";" =>
        Decl::external(name, params, ty),
    "extern" <name:Ident> "()" "->" <ty:Type> ";" =>
        Decl::external(name, vec![], ty),
    Func => { Decl::Func(<>) },
};

pub LamTyVars: Vec<String> = {
    "<" <tyvars: List<Ident>> ">" => tyvars,
                                  => vec![]
};

pub Func: Bind = {
    "let" <name:Ident> "(" <p:List<Ident>> ")" "{" <body: Expr> "}" => {
        Bind::Rec(name, Expr::Lam(Box::new(Lam::new(p, body))))
    },
    "let" <name:Ident> "()" "{" <body: Expr> "}" => {
        Bind::Rec(name, Expr::Lam(Box::new(Lam::new(vec![], body))))
    }
};

pub Param: (String,Type) = {
    <id:Ident> ":" <ty:Type> => (id,ty)
};

pub Var: Expr = {
    Ident   => Expr::Var(<>),
};

pub Caller: Expr = {
    Var   => <>
};

pub Lam: Lam = {
    r"\\" <p:Ident> "->" <body: Expr> => Lam::new(vec![p], body),
    r"\\" "(" <p:List<Ident>> ")" "->" "{" <body: Expr> "}" => Lam::new(p, body),
};

pub Expr: Expr = {
    Num     => Expr::I32Lit(<>),
    "true"  => Expr::BoolLit(true),
    "false" => Expr::BoolLit(false),
    "()"    => Expr::UnitLit,
    Var     => <>,
    Lam     => Expr::Lam(Box::new(<>)),
    "{" <t:Expr> "}" => t,
    "if" <cond:Expr> "{" <true_expr:Expr> "}" "else" "{" <false_expr:Expr> "}"
        => Expr::If(Box::new(If::new(cond, true_expr, false_expr))),
    <caller:Caller> "(" <args:List<Expr>> ")" 
        => Expr::App(Box::new(caller), args),
    <caller:Caller> "()" 
        => Expr::App(Box::new(caller), vec![]),
    "let" <id:Ident> "=" <e1:Expr> ";" <e2:Expr>
        => { let bind = Bind::NonRec(id, e1 );
             Expr::Let(Box::new(bind), Box::new(e2)) },
};

BaseType: Type = {
    "()"   => Type::Con("unit".to_string(), Kind::Star),
    Ident  => Type::Con(<>, Kind::Star)
};

Type: Type = {
    BaseType  => <>,
    ArrowType => {
        let mut params = <>;
        let retty  = params.pop().unwrap();
        func(params, retty)
    }
};


ArrowType: Vec<Type> = {
    <p:BaseType> "->" <r:BaseType>  => {
        vec![p, r]
    },
    <p:ArrowType> "->" <r:BaseType>  => {
        let mut p = p;
        p.push(r);
        p
    },
};

